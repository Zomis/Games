buildscript {
    ext.kotlin_version = '1.4.32'
    ext.klog_version = '2.0.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'net.zomis'
version '1.0-SNAPSHOT'

apply plugin: 'kotlin-platform-js'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "http://www.zomis.net/maven" }
    maven { url 'https://jitpack.io' } // KLog
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlin_version"
    expectedBy project(':games-core')

    compile "com.github.lewik.klog:klog-js:$klog_version"
    compile "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:1.4.3"
}

compileKotlin2Js {
    // kotlinOptions.moduleKind = "commonjs"
}

task assembleWeb(type: Sync) {
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                path.endsWith(".js") && (path.startsWith("META-INF/resources/") ||
                        !path.startsWith("META-INF/"))
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${projectDir}/web"

    dependsOn classes
}
task adjustDependencies {
    dependsOn assembleWeb
    doLast {
        def file = new File("${projectDir}/web/games-js.js")
        def text = file.getText("UTF-8")
        String searchString = "this\\['uttt-js']"
        def replaceWith = "window['uttt-js']"
        text = text.replaceAll(searchString, replaceWith)
        file.write(text, "UTF-8")
    }
}
assemble.dependsOn adjustDependencies
